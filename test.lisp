(defpackage :opengl-text-test (:use :cl :opengl-text :sdl :iterate))

(in-package :opengl-text-test)

(defun launch-sdl-opengl (w h)
  (init *init-video*)
  (gl-set-attribute +gl-red-size+ 8)
  (gl-set-attribute +gl-green-size+ 8)
  (gl-set-attribute +gl-blue-size+ 8)
  (gl-set-attribute +gl-alpha-size+ 8)
  (gl-set-attribute +gl-buffer-size+ 32)
  (gl-set-attribute +gl-depth-size+ 24)
  (gl-set-attribute +gl-doublebuffer+ 1)
  (set-video-mode w h 32 *opengl*)
  (cl-opengl:shade-model :smooth)
  (cl-opengl:clear-color 0 0 0 0.5)
  (cl-opengl:clear-depth 1.0)
  (cl-opengl:enable :depth-test)
  (cl-opengl:depth-func :lequal)
  (cl-opengl:hint :perspective-correction-hint :nicest)
  (cl-opengl:viewport 0 0 w h)
  (cl-opengl:matrix-mode :projection)
  (cl-opengl:load-identity)
  (cl-glu:perspective 45 (/ w h) 0.1 100)
  (cl-opengl:matrix-mode :modelview)
  (cl-opengl:load-identity))

(defun test-simple-draw-string (str)
  (gl:enable :texture-2d)
  (gl:enable :blend)
  (gl:blend-func :src-alpha :one-minus-src-alpha)
  (gl:enable-client-state :vertex-array)
  (gl:enable-client-state :texture-coord-array)
  (gl:clear :color-buffer-bit :depth-buffer-bit)
  (gl:matrix-mode :modelview)
  (gl:load-identity)
  (gl:translate -3 0 -8)
  (zpb-ttf:with-font-loader (font "/usr/share/fonts/dejavu/DejaVuSerif.ttf")
   (let ((gl-text (make-instance 'opengl-text :font font)))
     (gl:bind-texture :texture-2d 0)
     (gl:with-primitive :quads
       (gl:color 0 0 1)
       (gl:vertex 0 0 0)
       (gl:vertex 1 0 0)
       (gl:vertex 1 1 0)
       (gl:vertex 0 1 0))
     (draw-gl-string str gl-text)
     (gl:translate 0 -0.4 0)
     (gl:rotate 30 0 0 1)
     (setf (color-of gl-text) (list 255 0 10))
     (setf (emsquare-of gl-text) 128)
     (draw-gl-string str gl-text :kerning nil)
     (gl:translate 2 -1 0)
     (gl:bind-texture :texture-2d (opengl-text::texture-number-of gl-text))
     (gl:tex-env :texture-env :texture-env-mode :decal)
     (gl:with-primitive :quads
       (gl:color 0 1 0)
       (gl:tex-coord 0 0)
       (gl:vertex -2 -2 -1)
       (gl:tex-coord 0 1)
       (gl:vertex -2 0 -1)
       (gl:tex-coord 1 1)
       (gl:vertex 0 0 -1)
       (gl:tex-coord 1 0)
       (gl:vertex 0 -2 -1))
     (gl-swap-buffers))))

(defun test-anim-draw-string (string)
  (gl:enable :texture-2d)
  (gl:enable :blend)
  (gl:blend-func :src-alpha :one-minus-src-alpha)
  (gl:enable-client-state :vertex-array)
  (gl:enable-client-state :texture-coord-array)
  (gl:matrix-mode :modelview)
  (zpb-ttf:with-font-loader (font "/usr/share/fonts/dejavu/DejaVuSerif.ttf")
   (let ((gl-text (make-instance 'opengl-text :font font)))
     (iter (for i from 0)
	   (until (iter (for ev next (poll-event))
			(thereis (sdl::quit-event-p ev))
			(until (eql 0 ev))))
	   (gl:clear :color-buffer-bit :depth-buffer-bit)
	   (gl:load-identity)
	   (gl:translate -1 0 -8)
	   (gl:scale 0.4 0.4 0.4)
	   (gl:with-pushed-matrix
	     (gl:rotate (mod i 360) 0 0 1)
	     (draw-gl-string string gl-text))
	   (gl:with-pushed-matrix
	     (gl:translate 0 -2 2)
	     (gl:rotate (mod i 360) 0 1 0)
	     (draw-gl-string string gl-text))
	   (gl-swap-buffers)
	   (sleep 0.01)))))
